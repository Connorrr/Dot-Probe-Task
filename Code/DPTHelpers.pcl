#File: 			DPTHelpers.pcl
#Description:	Contains the additional subroutines used in the experiment
#Programmer:	Connor Reid (Psy Technician)
#Created@:		15/07/2016
#LastMod@:		15/07/2016
#ModifiedBy:	Connor Reid
#Copyright@: 	School Of Psychology, Griffith University
#-------------------------------------------------------------------

sub print_2d_array(array <string, 2>& arr) begin		##Used to terminal print 2d arrays
	loop int j = 1 until j > arr.count() begin
		term.print(string(j) + ": ");
		loop int k = 1 until k > arr[j].count() begin
			term.print(arr[j][k] + "\t");
			k = k + 1;
		end;
		term.print("\n");
		j = j + 1;
	end;
end;

sub
	get_input_csv(string file_path, array <string, 2>& stim)		# Get stimulus from this multi column file.  
begin
	stim.resize(0);
	
	input_file in_stim = new input_file;

	in_stim.open(file_path);

	in_stim.set_delimiter( ',' ); 	# for get_line()

	array <string> tmpstim[0];
		
	loop int count = 1 until
		in_stim.end_of_file() || count == 100 || !in_stim.last_succeeded()
	begin
		in_stim.get_string().split(",",tmpstim);
		if (tmpstim[1] != "")then
			stim.add(tmpstim);
			tmpstim.resize(0);
		end;
		count = count + 1;
	end;

	print_2d_array(stim);
end;

####  Get response after main trial has been presented.  Returns the reation time.
sub getResponse(array <int, 1> &resp, int side) begin
	if (bool( response_manager.response_count() )) then		#  If a key is pressed
		stimulus_data last = stimulus_manager.last_stimulus_data();		#  Last button Pressed
		resp[1] = last.reaction_time();
		if (side == 1 && last.button() == 1) then
			resp[2] = 1;	#  Correct
		elseif(side == 2 && last.button() == 2) then
			resp[2] = 1;	#  Correct
		else
			resp[2] = 0;
		end;
		resp[3] = last.button();
	end;
end;

## Save Output Data  Trial No,Trial Type,Top Word,Bottom Word,Response,RT,Correct
sub
saveTrialData(output_file out,int trialNo,string trialType,string topWord,string bottomWord,int resp,int rt,int corr)
begin
	out.print(trialNo);
	out.print(",");
	out.print(trialType);
	out.print(",");
	out.print(topWord);
	out.print(",");
	out.print(bottomWord);
	out.print(",");
	out.print(resp);
	out.print(",");
	out.print(rt);
	out.print(",");
	out.print(corr);
	out.print("\n");
end;

## Used to present trials for either the practice or main block
## Takes stim array and the Trial type String and output file
sub presentTrials(array <string, 2>& stim, string trialType, output_file out) begin
	int i=0;
	loop 
		i = 1;
	until 
		i > stim.count()		#  Loop through rows in csv
	begin
		int arrow = random(1,2);		#  Direction, left for 1 and right for 2
		array <int> resp[3] = {0,0,0};	# RT, isCorrect, Button Pressed
		
		fixation_trial.present();
		
		top_text.set_caption(stim[i][1]);
		bottom_text.set_caption(stim[i][2]);
		top_text.redraw();
		bottom_text.redraw();
		
		main_trial.present();
		
		if(arrow == 1) then
			response_text.set_caption("<");
			response_text.redraw();
		else
			response_text.set_caption(">");
			response_text.redraw();
		end;
		
		response_trial.present();
		getResponse(resp,arrow);
		
		saveTrialData(out,i,trialType,stim[i][1],stim[i][2],resp[3],resp[1],resp[2]);
		
		i = i + 1;
	end;
end;